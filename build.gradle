buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = "0.11.1-1.20.1"
group = "ttv.migami"
archivesBaseName = "JustEnoughGuns"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "jeg.refmap.json"
    config 'jeg.mixins.json'
}

minecraft {
    //mappings channel: 'official', version: '1.20.1'
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                jeg {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-mixin.config=jeg.mixins.json'
            mods {
                jeg {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'jeg', '--all', '--output', file('src/generated/resources/')
            arg '-mixin.config=jeg.mixins.json'
            mods {
                jeg {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.3.0'
    implementation fg.deobf('curse.maven:framework-549225:5692398')
    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.20.1:4.4.4')
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:15.2.0.27")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-forge-api:15.2.0.27")
    runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.2.0.27")

    compileOnly fg.deobf('curse.maven:controllable-317269:4598985')
    compileOnly fg.deobf('curse.maven:valkyrien-skies-258371:5212232')
    compileOnly fg.deobf('curse.maven:migamis-devil-fruits-996081:5798735')
    compileOnly fg.deobf('curse.maven:yungs-better-nether-fortresses-817651:5193465')
    //runtimeOnly fg.deobf('curse.maven:yungs-better-nether-fortresses-817651:5193465')
    //runtimeOnly fg.deobf('curse.maven:yungs-api-421850:5769971')
    //runtimeOnly fg.deobf('curse.maven:migamis-devil-fruits-996081:5798735')

    compileOnly fg.deobf('curse.maven:villager-gunners-973756:5703138')
    compileOnly fg.deobf('curse.maven:guard-villagers-360203:6020730')
    compileOnly fg.deobf('curse.maven:recruits-523860:5791566')

    /*runtimeOnly fg.deobf('curse.maven:villager-gunners-973756:5703138')
    runtimeOnly fg.deobf('curse.maven:guard-villagers-360203:6020730')
    runtimeOnly fg.deobf('curse.maven:recruits-523860:5791566')*/

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.6"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.3.6")) {
        jarJar.ranged(it, "[0.3.6,)")
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Just Enough Guns",
                "Specification-Vendor"    : "MigaMi",
                "Specification-Version"   : "1",
                "Implementation-Title"    : "Just Enough Guns",
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "MigaMi",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "jeg.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///X:/localmaven/mcmods"
        }
    }
}
